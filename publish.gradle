// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'
afterEvaluate {
    publishing {
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/sharechat/exoplayer")

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())

                credentials {
                    username = properties.getProperty('GITHUB_USERNAME')
                    password = properties.getProperty("GITHUB_TOKEN")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId = 'in.mohalla.exoplayer'
                artifactId = releaseArtifact
                version releaseVersion
                pom {
                    name = releaseArtifact
                    description = releaseDescription
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Open Source Project'
                        }
                    }
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
